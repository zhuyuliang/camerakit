apply plugin: 'com.android.library'
/*
 *  UVCCamera
 *  library and sample to access to UVC web camera on non-rooted Android device
 *
 * Copyright (c) 2014-2017 saki t_saki@serenegiant.com
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *   You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 *
 *  All files in the folder are under this Apache License, Version 2.0.
 *  Files in the libjpeg-turbo, libusb, libuvc, rapidjson folder
 *  may have a different license, see the respective files.
 */

import org.apache.tools.ant.taskdefs.condition.Os

android {
	compileSdkVersion rootProject.ext.android["compileSdkVersion"]
	buildToolsVersion rootProject.ext.dependVersion["supportLibraryVersion"]

	defaultConfig {
		minSdkVersion rootProject.ext.android["minSdkVersion"]
		targetSdkVersion rootProject.ext.android["targetSdkVersion"]
		versionCode 4
		versionName "1.0.3"
		ndk {
			abiFilters 'armeabi-v7a','arm64-v8a'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

    defaultConfig {
        minSdkVersion 16
		targetSdkVersion rootProject.ext.android["targetSdkVersion"]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
	sourceSets {
		main {
			jniLibs.srcDir 'src/main/libs'
			jni.srcDirs = []
		}
	}
	lintOptions {
		abortOnError false
	}

	libraryVariants.all { variant ->
		if (variant.buildType.name == "release") {
			variant.outputs.all {
				//${variant.flavorName}
				outputFileName = "${project.name}-v${defaultConfig.versionName}-${variant.buildType.name}.aar"
			}
		}
		if (variant.buildType.name == "release") {
			variant.getPackageLibraryProvider().get().destinationDir = new File(project.rootDir.absolutePath + "/repositories")
		}
	}
}

tasks.withType(JavaCompile) {
	compileTask -> compileTask.dependsOn ndkBuild
}

String getNdkBuildPath() {
	Properties properties = new Properties()
	properties.load(project.rootProject.file('local.properties').newDataInputStream())
	def ndkBuildingDir = properties.getProperty("ndk.dir")
	def ndkBuildPath = ndkBuildingDir
	if (Os.isFamily(Os.FAMILY_WINDOWS)) {
		ndkBuildPath = ndkBuildingDir + '/ndk-build.cmd'
	} else {
		ndkBuildPath = ndkBuildingDir + '/ndk-build'
	}
	return ndkBuildPath
}

task ndkBuild(type: Exec, description: 'Compile JNI source via NDK') {
	println('executing ndkBuild')
	def ndkBuildPath = getNdkBuildPath();
	commandLine ndkBuildPath, '-j8', '-C', file('src/main').absolutePath
}

task ndkClean(type: Exec, description: 'clean JNI libraries') {
	println('executing ndkBuild clean')
	def ndkBuildPath = getNdkBuildPath();
	commandLine ndkBuildPath, 'clean', '-C', file('src/main').absolutePath
}

clean.dependsOn 'ndkClean'

dependencies {
    implementation fileTree(dir: new File(buildDir, 'libs'), include: '*.jar')

//	implementation "com.android.support:support-v4:28.0.0"
//	implementation "com.android.support:support-annotations:28.0.0"

	implementation("com.serenegiant:common:2.12.4") {
		exclude module: 'support-v4'
	}

	implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
	implementation 'io.reactivex.rxjava2:rxjava:2.2.14'
}
